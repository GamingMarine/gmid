ACTOR CacoColaBottleAdd : Health
{
	Inventory.Amount 1
}
ACTOR DidHeal : Inventory {}
ACTOR CacoColaBottle : CustomInventory replaces ArtiSuperHealth
{
	+INVBAR
	Inventory.Amount 100
	Inventory.MaxAmount 100
	Inventory.InterHubAmount 100
	Inventory.PickupMessage "$GOTBOTTLE"
	Tag "$TAG_BOTTLE"
	Inventory.PickupSound "PCOLABUP"
	Inventory.Icon "COLAI0"
	Scale 0.6
	States
	{
		Spawn:
		COLA H -1
		Stop
		Use:
		GiveCheck:
		TNT1 A 0 A_JumpIfInventory("CacoColaBottle", 1, "GiveCheck2")
		TNT1 A 0 A_JumpIfInventory("DidHeal", 1, "PlaySound")
		Fail
		GiveCheck2:
		TNT1 A 0 A_JumpIf(CallACS("Check max health") == TRUE, "Give")
		TNT1 A 0 A_JumpIfInventory("DidHeal", 1, "PlaySound")
		TNT1 A 0 A_PlaySound("inventory/cantuse")
		TNT1 A 0 A_Print("$BOTTLENOPE")
		Fail
		PlaySound:
		TNT1 A 0 A_TakeInventory("Didheal", 1)
		TNT1 A 0 A_PlaySound("FKITUSE")
		TNT1 A 0 A_SetBlend("23 07 06",0.75,16)
		Fail	
		Give:
		TNT1 A 0 A_GiveInventory("DidHeal", 1)
		GiveHealth:
		TNT1 A 0 A_TakeInventory("CacoColaBottle", 1)
		TNT1 A 0 A_GiveInventory("CacoColaBottleAdd", 1)
		Goto GiveCheck
	}
}

Actor NewRedCard : CustomInventory replaces RedCard
{
  Inventory.PickupSound "misc/k_pkup"
  Inventory.PickupMessage "$GOTREDCARD2"
  States
  {
  Spawn:
	RKEY A 10
	RKEY B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("RedCard",1)
	Stop
  }
}

Actor NewRedSkull : CustomInventory replaces RedSkull
{
  Inventory.PickupSound "misc/skullup"
  Inventory.PickupMessage "$GOTREDSKUL2"
  States
  {
  Spawn:
	RSKU A 10
	RSKU B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("RedSkull",1)
	Stop
  }
}

Actor NewBlueCard : CustomInventory replaces BlueCard
{
  Inventory.PickupSound "misc/k_pkup"
  Inventory.PickupMessage "$GOTBLUECARD2"
  States
  {
  Spawn:
	BKEY A 10
	BKEY B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("BlueCard",1)
	Stop
  }
}

Actor NewBlueSkull : CustomInventory replaces BlueSkull
{
  Inventory.PickupSound "misc/skullup"
  Inventory.PickupMessage "$GOTBLUESKUL2"
  States
  {
  Spawn:
	BSKU A 10
	BSKU B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("BlueSkull",1)
	Stop
  }
}


Actor NewYellowCard : CustomInventory replaces YellowCard
{
  Inventory.PickupSound "misc/k_pkup"
  Inventory.PickupMessage "$GOTYELWCARD2"
  States
  {
  Spawn:
	YKEY A 10
	YKEY B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("YellowCard",1)
	Stop
  }
}

Actor NewYellowSkull : CustomInventory replaces YellowSkull
{
  Inventory.PickupSound "misc/skullup"
  Inventory.PickupMessage "$GOTYELWSKUL2"
  States
  {
  Spawn:
	YSKU A 10
	YSKU B 10 Bright
	Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("YellowSkull",1)
	Stop
  }
}

//Heretic Keys
Actor YellowPrismKey : CustomInventory replaces KeyYellow
{
  Inventory.PickupSound "misc/prismup"
  Inventory.PickupMessage "$GOTYELWPRISM"
  +FLOATBOB
  States
  {
  Spawn:
    CKYY ABCDEFGHI 3 Bright
    Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("KeyYellow",1)
	Stop
  }
}

Actor GreenPrismKey : CustomInventory replaces KeyGreen
{
  Inventory.PickupSound "misc/prismup"
  Inventory.PickupMessage "$GOTGREENPRISM"
  +FLOATBOB
  States
  {
  Spawn:
    AKYY ABCDEFGHI 3 Bright
    Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("KeyGreen",1)
	Stop
  }
}

Actor BluePrismKey : CustomInventory replaces KeyBlue
{
  Inventory.PickupSound "misc/prismup"
  Inventory.PickupMessage "$GOTBLUEPRISM"
  +FLOATBOB
  States
  {
  Spawn:
    BKYY ABCDEFGHI 3 Bright
    Loop
  Pickup:
	TNT1 A 1 A_GiveInventory("KeyBlue",1)
	Stop
  }
}

Actor DeactivatedSentry : CustomInventory
{
	Inventory.MaxAmount 3
	Inventory.InterHubAmount 3
	Inventory.PickupSound "sentry/pickup"
	Inventory.UseSound "sentry/pickup"
	Inventory.PickupMessage "$GOTSENTRY"
	Inventory.Icon "SNTPZ0"
	Tag "$TAG_SENTRY"
	+INVBAR
	States
	{
	Spawn:
		SNTP A -1
		Stop
	Use:
		SNTP A 0 A_ThrowGrenade("ThrownSentry",2,8,2,0)
		TNT1 A 0 A_Print("$SENTRYNOTE")
		Stop
	}
}

Actor ThrownSentry
{
  +DROPOFF
  +MISSILE
  +WINDTHRUST
  +CANBOUNCEWATER
  DeathSound "sentry/deploy"
  States
  {
  Spawn:
	SNTP A 1
	Loop
  Death:
	SNTP A 0 A_Scream
	SNTP A 0 A_SpawnItem("Sentry")
	Stop
  } 
}

Actor Sentry : SwitchingDecoration
{
  Health 250
  Radius 20
  Height 56
  Mass 100000
  Speed 0
  MaxTargetRange 2048
  MinMissileChance 256
  Activation THINGSPEC_Activate
  AttackSound "sentry/fire"
  MONSTER
  +FLOORCLIP
  +FRIENDLY
  +NOBLOOD
  +LOOKALLAROUND
  +MISSILEMORE
  +MISSILEEVENMORE
  +NEVERRESPAWN
  +STANDSTILL
  +USESPECIAL
  States
  {
  Spawn:
	SNTP A 5
	SNTP A 0 A_PlaySound("sentry/arm")
	SNTP BCDEFGHIJKLMNOPQR 2
	Goto Look
  Look:
    SENT AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4 A_Look
	SENT A 0 A_PlaySound("sentry/idle")
	SENT A 0 A_Jump(256, "Turn1", "Turn2")
	Loop
  Turn1:
	SENT A 0 A_SetAngle(45+angle)
	Goto Look
  Turn2:
	SENT A 0 A_SetAngle(-45+angle)
	Goto Look
  See:
	SENT AAAAAAAAA 4 A_Chase
	SENT A 0 A_PlaySound("sentry/ping")
	Loop
  Active:
	SNTP A 0 A_PlaySound("sentry/deactivate")
	SNTP RQPONMLKJIHGFEDCB 2
	SNTP A 0 A_SpawnItem("DeactivatedSentry")
	Stop
  Missile:
	SENT A 0 A_PlaySound("sentry/alarm")
    SENT AAAAAAAA 2 A_FaceTarget
	SENT C 0 A_PlaySound("sentry/fire", CHAN_WEAPON)
	SENT C 2 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "SentryPuff", 0, CBAF_NORANDOM)
    SENT B 2 Bright A_CposRefire
    goto Missile+9
  Death:
	ZXPL A 0 A_PlaySound("explosion/distant", CHAN_AUTO)
	ZXPL A 0 A_PlaySound("newexplosion")
	ZXPL A 0 A_Explode(175,148)
	ZXPL A 0 A_SpawnItem("ExplosionSpawner")
	TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("BarrelParts",frandom(5.0,-5.0),frandom(5.0,-5.0),frandom(0.5,10.0),frandom(2.0,8.0),0,random(6.0,12.0),random(0,360))
	Stop
  }
}

Actor SentryPuff : XaserPuff
{
  DamageType "Sentry"
}

Actor SkullBomb : CustomInventory
{
	Inventory.MaxAmount 1
	Inventory.PickupSound "skullbomb/pickup"
	Inventory.UseSound "skullbomb/throw"
	Inventory.PickupMessage "$GOTSKULL"
	Inventory.Icon "I_SBG0"
	Tag "$TAG_SKULL"
	+INVBAR
	Scale 0.5
	States
	{
	Spawn:
		SBMB Z -1
		Stop
	Use:
		SBMB Z 0 A_ThrowGrenade("ThrownSkullBomb",2,8,2,0)
		Stop
	}
}

Actor ThrownSkullBomb
{
  +DROPOFF
  +MISSILE
  +WINDTHRUST
  +CANBOUNCEWATER
  Scale 0.5
  States
  {
  Spawn:
	SBMB Z 1
	Loop
  Death:
	SBMB Z 0
	SBMB Z 0 A_Print("$SKULLWARN")
	SBMB Z 0 A_SpawnItem("ActivatedSkullBomb")
	Stop
  } 
}

Actor ActivatedSkullBomb
{
  Scale 0.5
  States
  {
  Spawn:
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_PlaySound("skullbomb/beep")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_PlaySound("skullbomb/beep")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_PlaySound("skullbomb/beep") //5 seconds
	SBMB A 0 A_Print("5...")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_PlaySound("skullbomb/beep")
	SBMB A 0 A_Print("4...")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_PlaySound("skullbomb/warning",CHAN_7,1,true)
	SBMB A 0 A_Print("\cg3...")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_Print("\cg2...")
	SBMB ABCDEFABCDEF 3 Bright
	SBMB A 0 A_Print("\cg1...")
	SBMB A 0 A_Jump(1,"SecretSegment")
	SBMB ABCDEFABCDE 3 Bright
	SBMB F 1 Bright
	SBMB A 0 A_StopSound(CHAN_7)
	SBMB A 0 A_Print("")
	SBMB A 0 A_PlaySound("skullbomb/explode")
	SBMB A 0 A_SpawnItem("SkullBombExplosion")
	SBMB A 0 ACS_NamedExecuteAlways("SkullBombFace")
	Stop
  SecretSegment:
	SBMB A 0 A_PlaySound("skullbomb/secret1", CHAN_AUTO)
	SBMB ABCDEFABCDE 3 Bright
	SBMB F 1 Bright
	SBMB A 0 A_StopSound(CHAN_7)
	SBMB A 0 A_Print("")
	SBMB A 0 A_PlaySound("skullbomb/explode")
	SBMB A 0 A_SpawnItem("SkullBombExplosion")
	SBMB A 0 ACS_NamedExecuteAlways("SkullBombFace")
	SBMB A 0 A_PlaySound("skullbomb/secret2", CHAN_AUTO)
	Stop
  }
}

Actor SkullBombExplosion
{
  Scale 5
  DamageType "SkullBomb"
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Quake(9,137,0,10000,"null")
	TNT1 A 0 A_Explode(4375,2000)
	SBXP ABCDEFGHIJKLMNOPQRSTU 7 Bright
	Stop
  }
}

Actor PowerFlight2 : PowerFlight
{
  Inventory.Icon "BELTC0"
}

//Z-Grav Belt actor has been moved to Heretic filter folder

Actor PowerTimeFreezer2 : PowerTimeFreezer
{
  Inventory.Icon "TADDC0"
}

//TADD actor has been moved to Heretic filter folder