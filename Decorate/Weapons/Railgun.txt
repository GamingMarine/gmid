Actor RailgunToken : Inventory {}

Actor GMIDRailgun : PlasmaRifle
{
	Weapon.AmmoUse 10
    Weapon.AmmoGive 40
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.UpSound "railup"
	Tag "$TAG_RAIL"
	Weapon.AmmoType1 "NewCell"
	Weapon.AmmoUse 10
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTRAIL"
	Obituary "$OB_RAIL"
	Inventory.Icon "RGNGA0B0"
	Weapon.SelectionOrder 200
	states
	{
		Fire:
			RGUN A 0 A_AlertMonsters
			RGUN A 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
			RGUN A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
			Goto ContinueFire
		QuadFire:
		    TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
			RGUN A 0 A_PlaySound("QuadDamage/Use")
			Goto ContinueFire
		DoubleFire:
		    TNT1 A 0 A_SetBlend("Red", 0.25, 16)
			RGUN A 0 A_PlaySound("DoubleDamage/Use")
		ContinueFire:
			RGUN A 0 A_SetPitch(pitch-1)
			RGUN B 0 BRIGHT A_RailAttack(300, 0, 1, "DarkGreen", "Green",0,0,"GMIDRailgunPuff")
			RGUN B 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGUN C 0 A_SetPitch(pitch+0.50)
			RGUN C 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGUN D 0 A_SetPitch(pitch+0.50)
			RGUN DEFGH 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGUN I 0 A_PlaySound("railgun/charge")
			RGUN I 40 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGUN HGFEDJ 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGUN K 1 A_WeaponReady
			goto Ready
		Ready:
			TNT1 A 0 A_JumpIfInventory("RailgunToken",1,"FirstTime")
			RGUN A 1 A_WeaponReady
			Loop
		FirstTime:
			TNT1 A 0 A_TakeInventory("RailgunToken",0)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("PVPCheck",0,0,0,0) == 2,"Ready")
			TNT1 A 0 A_JumpIf(CallACS("PickupAnimation")==0, "Ready")
			RGUN A 2 A_WeaponReady
			TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
			RGFS ABCDEFFFFF 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGFS G 0 A_PlaySound("railgun/fiddle1")
			RGFS GFGFGFFF 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGFS H 0 A_PlaySound("railgun/fiddle2")
			RGFS FHIJKKKKKKK 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGFS L 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
			RGFS LM 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGFS N 0 A_PlaySound("railgun/fiddle3")
			RGFS N 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			RGFS OPQRSTUVWXYA 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			Goto Ready
		Deselect:
			RGUN A 1 A_Lower
			RGUN A 0 A_Lower
			Loop
		Select:
			RGUN A 1 A_Raise
			RGUN A 0 A_Raise
			Loop
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128, "Spawn2")
			RGNG A -1
			Stop
		Spawn2:
			RGNG B -1
			Stop
	    AltFire:
			TNT1 A 0 A_GunFlash("WeaponStill")
			TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
			FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
			FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
			Goto ContinueKick
		QuadKick:
			TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
			FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
			Goto ContinueKick
		DoubleKick:
			TNT1 A 0 A_SetBlend("Red", 0.25, 16)
			FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
		ContinueKick:
			FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
			FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			FOOT D 0 A_Quake(1,2,0,10,"")
			FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			TNT1 A 8 A_WeaponReady(WRF_NoFire)
			TNT1 A 0 A_ReFire
			Goto Ready
		WeaponStill:
			RGUN A 22
			Goto LightDone
	}
}

Actor GMIDRailgunPuff : BulletPuff
{
  DamageType "GMIDRailgun"
  States
  {
  Spawn:
	TNT1 A 0
	Stop
  }
}