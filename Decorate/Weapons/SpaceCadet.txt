Actor SpaceCadet : BFG9000
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.UpSound "bfgup"
	Inventory.PickupSound "cadet/pickup"
	Tag "$TAG_PINBALL"
	Weapon.AmmoType1 "SmallMana"
	Weapon.AmmoUse 2
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTPINBALL"
	Obituary "$OB_PINBALL"
	Scale 0.3125
	+FLOATBOB
	+FORCEXYBILLBOARD
	Weapon.SelectionOrder 5000
	states
	{
  Ready:
	PING A 1 A_WeaponReady
	Loop
  Fire:
	PING BC 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
  Hold:
	PING A 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
	PING A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
	Goto ContinueFire
  QuadFire:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	PING A 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire
  DoubleFire:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	PING A 0 A_PlaySound("DoubleDamage/Use")
  ContinueFire:
	PING A 0 A_AlertMonsters
	PING D 0 A_FireCustomMissile("IveGotBigBalls")
	PING D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	PING EFGHGFE 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	PING D 0 A_ReFire("Hold")
	PING D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	PING CB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	goto Ready
  Deselect:
    PING A 1 A_Lower
    PING A 0 A_Lower
    Loop
  Select:
    PING A 1 A_Raise
    Loop
  Spawn:
    PINB B -1 Bright
    Stop
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire)
	TNT1 A 0 A_ReFire
	Goto Ready
  WeaponStill:
	PING A 22
	Goto LightDone
	}
}

ACTOR IveGotBigBalls //My attempt at an AC/DC reference
{
	Radius 6
	Height 8
	Projectile
	Speed 50
	Damage 10
	SeeSound "bigball/fire"
	DeathSound "bigball/hit"
	DamageType "Basic"
	Obituary "$OB_PINBALL"
	Scale 0.3125
	+FORCEXYBILLBOARD
	States
	{
	  Spawn:
		PINB A 1 Bright
		Loop
	  Death:
		TNT1 A 0 A_Explode(100, 128)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("IveGotSmallBalls",frandom(5.0,-5.0),frandom(5.0,-5.0),frandom(0.5,10.0),frandom(2.0,8.0),0,random(6.0,12.0),random(0,360))
		Stop
	}
}

ACTOR IveGotSmallBalls
{
   Radius 2
   Height 2
   Speed 0
   Damage 0
   Scale 0.3
   ReactionTime 2
   SeeSound ""
   PROJECTILE
   -NOGRAVITY
   +HEXENBOUNCE
   +FORCEXYBILLBOARD
   Scale 0.15625
   BounceSound "bigball/bounce"
   DeathSound "bigball/bounce"
   States
   {
   Spawn:
      TNT1 A 0 A_Countdown
      PINB AAAAAAAAAA 2 Bright
      TNT1 A 0 A_Jump(128,2)
      TNT1 A 0 A_Countdown
      PINB AAAAAAAAAA 2 Bright
      Loop
   Death:
      TNT1 A 1
      Stop
   }
}