Actor GMIDShotgunToken : Inventory {}

Actor GMIDShotgun : Shotgun
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.UpSound "shotcock"
	attacksound ""
	Weapon.AmmoUse 1
	+WEAPON.NOALERT
	Tag "$TAG_BOOMSTICK"
	Inventory.PickupMessage "$GOTBOOMSTICK"
	Obituary "$OB_BOOMSTICK"
	Inventory.Icon "SHOTA0B0"
	states
	{
		  Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128, "Spawn2")
			SHOT A -1 
			Stop
		  Spawn2:
			SHOT B -1
			Stop
		Fire:
			SHTG A 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG A 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
			SHTG A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
			Goto ContinueFire
		QuadFire:
			TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
			SHTG A 0 A_PlaySound("QuadDamage/Use")
			Goto ContinueFire
		DoubleFire:
			TNT1 A 0 A_SetBlend("Red", 0.25, 16)
			SHTG A 0 A_PlaySound("DoubleDamage/Use")
		ContinueFire:
			SHTG A 0 A_AlertMonsters
			SHTG A 0 A_SetPitch(pitch-0.5)
			SHTG A 0 A_PlaySound("weapons/GMSHOTGN", CHAN_WEAPON)
			SHTG A 0 A_Light1
			SHTG A 0 A_FireBullets (5.6, 0, 7, 5, "XaserPuff")
			SHTG B 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG C 0 A_SetPitch(pitch+0.25)
			SHTG C 0 A_Light2
			SHTG C 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG D 0 A_SetPitch(pitch+0.25)
			SHTG D 0 A_Light0
			SHTG DEFGHS 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG HI 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG JK 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			TNT1 A 0 A_PlaySound("Shotgun/Pump1", CHAN_AUTO)
			SHTG L 0 A_FireCustomMissile("ShotgunCasingSpawner",0,0,17,0)
			SHTG LMM 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG M 0 A_PlaySound("Shotgun/Pump2", CHAN_AUTO)
			SHTG MN 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG OP 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG QR 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG A 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG A 1 A_WeaponReady
			goto Ready
		Ready:
			TNT1 A 0 A_JumpIfInventory("GMIDShotgunToken",1,"FirstTime")
			SHTG A 1 A_WeaponReady
			Loop
		Deselect:
			SHTG A 1 A_Lower
			SHTG A 0 A_Lower
			Loop
		Select:
			SHTG A 1 A_Raise
			SHTG A 0 A_Raise
			Loop
		 FirstTime:
			TNT1 A 0 A_TakeInventory("GMIDShotgunToken",9999)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("PVPCheck",0,0,0,0) == 2,"Ready")
			TNT1 A 0 A_JumpIf(CallACS("PickupAnimation")==0, "Ready")
			SHTG A 2 A_WeaponReady
			TNT1 A 0 A_PlaySound("weapon/fiddle")
			SGFS ABCDDDEFG 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SGFS H 0 A_PlaySound("shellclip/get")
			SGFS HIJDEFG 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SGFS H 0 A_PlaySound("shellclip/get")
			SGFS HIJDEFG 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SGFS H 0 A_PlaySound("shellclip/get")
			SGFS HIJD 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			TNT1 A 0 A_PlaySound("weapon/fiddle")
			SGFS CBA 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG ARQP 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG O 0 A_PlaySound("Shotgun/Pump1", CHAN_AUTO)
			SHTG ONT 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			SHTG T 0 A_PlaySound("Shotgun/Pump2", CHAN_AUTO)
			SHTG TNOPQR 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
			Goto Ready
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_ReFire
	Goto Ready
		WeaponStill:
			SHTG A 22
			Goto LightDone
		}
}