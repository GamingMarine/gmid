Actor HasSoulCube : Inventory {}

Actor SoulCube : weapon
{
   Weapon.BobRangeX 0.3
   Weapon.BobRangeY 0.5
   Weapon.BobStyle InverseSmooth
   Weapon.BobSpeed 2.0
   Weapon.SlotNumber 3
   Weapon.SelectionOrder 350
   Inventory.PickupSound "SoulCube/Pickup"
   Obituary "$OB_CUBE"
   Tag "$TAG_CUBE"
   Weapon.SlotNumber 7
   +WEAPON.NOALERT
   +WEAPON.NO_AUTO_SWITCH
   +FORCEXYBILLBOARD
   AttackSound "SoulCube/Use" 
   damagetype Cube
   Scale 0.5
   States 
   { 
   Spawn: 
      SCBG A -1
      Stop
   Ready: 
      SCUB A 0 A_JumpIfInventory ("CubeAmmo", 50, "UseUs")
      SCUB A 1 A_WeaponReady
      Loop
   UseUs:
	  SCUB A 0 A_Print("$CUBEREADY")
	  SCUB A 0 A_PlaySound("SoulCube/UseUs")
	  SCUB ABCDEFGHI 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  Goto ActuallyReady
   ActuallyReady:
      SCUB I 1 A_WeaponReady
      Loop 
   Deselect: 
      SCUB A 1 A_Lower
	  SCUB A 0 A_Lower
      Loop 
   Select: 
      SCUB A 1 A_Raise
	  SCUB A 0 A_Raise
      Loop 
   Fire:
      SCUB A 0 A_JumpIfInventory ("CubeAmmo", 50, "Fire2")
	  SCUB A 0 A_PlaySound("SoulCube/NotYet")
      SCUB A 0 A_Print("$CUBENOPE")
	  SCUB A 5 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
      Goto Ready
   Fire2:
	  SCUB J 0 A_AlertMonsters
	  SCUB D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
	  SCUB D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
	  Goto ContinueFire
   QuadFire:
	  TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	  SCUB D 0 A_PlaySound("QuadDamage/Use")
	  Goto ContinueFire
   DoubleFire:
	  TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	  SCUB D 0 A_PlaySound("DoubleDamage/Use")
   ContinueFire:
      SCUB J 0 A_FireCustomMissile("SoulCubeProj",0,0,0,0,0)
	  SCUB J 5 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  SCUB J 1 Offset(0,10)
	  SCUB J 1 Offset(0,20)
	  SCUB J 1 Offset(0,30)
	  SCUB J 1 Offset(0,40)
	  SCUB J 1 Offset(0,50)
	  SCUB J 1 Offset(0,60)
	  SCUB J 1 Offset(0,70)
	  SCUB J 1 Offset(0,80)
	  SCUB J 1 Offset(0,90)
      SCUB P 0 A_TakeInventory("SoulCube")
      Goto Ready 
  AltFire:
	  TNT1 A 0 A_GunFlash("WeaponStill")
	  TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	  FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	  FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	  Goto ContinueKick
  QuadKick:
	  TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	  FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	  Goto ContinueKick
  DoubleKick:
	  TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	  FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	  FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	  FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  FOOT D 0 A_Quake(1,2,0,10,"")
	  FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	  TNT1 A 8 A_WeaponReady(WRF_NoFire)
	  TNT1 A 0 A_ReFire
	  TNT1 A 0 A_JumpIfInventory("CubeAmmo", 50, "ActuallyReady")
	  Goto Ready
  WeaponStill:
	  SCUB A 0 A_JumpIfInventory("CubeAmmo", 50, "WeaponStill2")
	  SCUB A 22
	  Goto LightDone
  WeaponStill2:
	  SCUB I 22
	  Goto LightDone
   } 
}
  
actor SoulCubeProj
{
  radius 13
  height 8
  speed 25
  damage 5000
  damagetype "Cube"
  seesound "SoulCube/Use"
  deathsound "SoulCube/Slice"
   Scale 0.25
  PROJECTILE
  +SKYEXPLODE
  +SCREENSEEKER
  +FORCEXYBILLBOARD
  states
  {
  Spawn:
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    loop
  Death:
    TNT1 A 0 A_CustomMissile ("SoulCubeReturnSpawner",0,0,0,0,0)
	TNT1 A 0 A_Quake(2,12,0,800)
	TNT1 A 0 A_Explode(175,256)
    TNT1 A -1
    stop
  }
}

actor SoulCubeReturnSpawner
{
  Radius 0
  Height 0
  Scale 0.25
  MONSTER
  +NONSHOOTABLE
  +NOGRAVITY
  -SOLID
  -COUNTKILL
  States
  {
  Spawn:
    SCBW A 0 A_PlaySound ("SoulCube/Slice")
    SCBW AAAAAAAAAAAAAAAA 1
  Missile:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("SoulCube/Use")
    TNT1 A 0 A_CustomMissile ("SoulCubeReturner",0,0,0,0,0)
  Death:
    TNT1 A 0 A_GiveToTarget ("SoulCube")
    TNT1 A 0 A_TakeFromTarget ("CubeAmmo", 50)
    TNT1 A -1
    Stop
  }
}
  
actor SoulCubeReturner : Ammo
{
  radius 13
  height 1
  speed 25
  damage 0
   Scale 0.25
  +MISSILE
  +STEPMISSILE
  +SEEKERMISSILE
  +PAINLESS
  +NONSHOOTABLE
  +NOGRAVITY
  +FORCEXYBILLBOARD
  -SOLID
  Tag "$TAG_CUBE"
  Inventory.PickupMessage "$GOTCUBE2"
  Inventory.PickupSound "SoulCube/GetSoul"
  states
  {
  Spawn:
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    SCBW A 1 A_SeekerMissile (90, 90, SMF_PRECISE)
    loop
  Death:
    TNT1 A -1
    stop
  Pickup:
    TNT1 A 0 A_GiveInventory("SoulCube")
    TNT1 A 0 A_TakeInventory("CubeAmmo", 50)
    stop
  }
}
  
actor SoulCubeGenerator : CustomInventory
{
  radius 13
  height 8
  +FLOORCLIP
  +FLOATBOB
  +AUTOACTIVATE
  +FORCEXYBILLBOARD
  Tag "$TAG_CUBE"
  Inventory.PickupMessage "$GOTCUBE"
  Inventory.PickupSound "SoulCube/Pickup" 
   Scale 0.25
  states
  {
  Spawn:
    SCBG A 5
	Loop
  Pickup:
	TNT1 A 0
	TNT1 A 0 A_JumpIfInventory("HasSoulCube",1,"FailIt")
	Goto Use
  Use:
	TNT1 A 0 A_GiveInventory("HasSoulCube")
    TNT1 A 0 A_GiveInventory("SoulCube")
    TNT1 A 0 A_TakeInventory("CubeAmmo", 25)
    TNT1 A 0 A_SelectWeapon("SoulCube") 
    Stop
  FailIt:
	TNT1 A 0
	TNT1 A 0
	Stop
  }
}

actor CubeAmmo : Ammo
{
	 Inventory.Amount 1
     Inventory.MaxAmount 50
     Ammo.BackpackAmount 0
     Ammo.BackpackMaxAmount 50
	 Tag "$TAG_CUBEAMMO"
	 Inventory.PickupMessage "$GOTCUBEAMMO"
	 Inventory.PickupSound "SoulCube/GetSoul"
	 Scale 0.25
	 Alpha 0.99
	 +FLOATBOB
	 +FORCEXYBILLBOARD
	 States
	 {
	 Spawn:
	   TERR ABCDE 3 Bright
	   Loop
	 }
}