Actor VGunToken : Inventory {}

Actor VGun : Chaingun
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.UpSound "chanup"
	Weapon.AmmoType1 "Clip3"
	Weapon.AmmoUse 1
	Tag "$TAG_VGUN"
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTVGUN"
	Obituary "$OB_VGUN"
	Inventory.Icon "MGUNA0"
	Weapon.SelectionOrder 700
	states
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, "Spawn2")
		MGUN A -1 
		Stop
		Spawn2:
		MGUN B -1
		Stop
		Fire: 
		CHGG A 0 A_AlertMonsters
		CHGG C 0 A_PlaySound("vgun/add", CHAN_6, 0.5, true)
		CHGG A 0 A_PlaySound("vgun/motor", CHAN_7, 1.0, true)
		Hold:
		CHGG A 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
		CHGG A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
		Goto ContinueFire
		QuadFire:
		TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
		CHGG A 0 A_PlaySound("QuadDamage/Use")
		Goto ContinueFire
		DoubleFire:
		TNT1 A 0 A_SetBlend("Red", 0.25, 16)
		CHGG A 0 A_PlaySound("DoubleDamage/Use")
		ContinueFire:
		CHGG C 0 A_Light1 
		CHGG C 0 A_PlaySound("weapons/UZIFIRE", CHAN_WEAPON)
		CHGG C 0 A_FireCustomMissile("RifleCasingSpawner",0,0,17,0)
		CHGG C 0 A_SetPitch(pitch-1)
		CHGG C 0 A_FireBullets(5.6, 0, 1, 5, "XaserPuff")
		CHGG C 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		CHGG D 0 A_SetPitch(pitch+1)
		CHGG D 0 A_Light0
		CHGG D 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		CHGG A 0 A_ReFire("Hold")
		CHGG A 0 A_PlaySound("vgun/addstop", CHAN_6, 0.5)
		CHGG A 0 A_PlaySound("vgun/winddown", CHAN_7)
		CHGG AABBAAAABBBBAAAAAAAABBBBBBBB 1 A_WeaponReady
		goto Ready
	Ready:
		TNT1 A 0 A_JumpIfInventory("VGunToken",1,"FirstTime")
		CHGG A 1 A_WeaponReady
		Loop
  Deselect:
		CHGG A 0 A_StopSound(CHAN_6)
		CHGG A 0 A_StopSound(CHAN_7)
		CHGG A 1 A_Lower
		CHGG A 0 A_Lower
		Loop
	Select:
		CHGG A 1 A_Raise
		CHGG A 0 A_Raise
		Loop
	FirstTime:
		TNT1 A 0 A_TakeInventory("VGunToken",0)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("PVPCheck",0,0,0,0) == 2,"Ready")
		TNT1 A 0 A_JumpIf(CallACS("PickupAnimation")==0, "Ready")
		CHGG A 2 A_WeaponReady
		TNT1 A 0 A_PlaySound("weapon/fiddle")
		CGFS ABC 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("vgun/fiddle1")
		CGFS DEEE 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
		CGFS FGHI 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
		CGFS JKLMN 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("vgun/fiddle2")
		CGFS OPQQQ 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
		CGFS RST 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		Goto Ready
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire)
	TNT1 A 0 A_ReFire
	Goto Ready
	WeaponStill:
		CHGG A 22
		Goto LightDone
	}
}

Actor Clip3 : Ammo replaces BlasterAmmo
{
	Game Doom
	+FLOORCLIP
	+DONTGIB
	Inventory.Amount 15
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 600
	Tag "$TAG_BULLET1"
	Inventory.PickupSound "bulletmagazine/get"
	Inventory.Icon "BLITC0"
	Inventory.PickupMessage "$GOTBULLET1"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, "Spawn2")
		BLIT A -1 
		Stop
	Spawn2:
		BLIT B -1
		Stop
	}
}

Actor Clip3_Used : Clip3 //This is here so that picking up a magazine from an enemy will display a different message
{
	Inventory.PickupMessage "$GOTBULLET_USED"
}

Actor ClipBox3 : Clip3 replaces BlasterHefty
{
	Inventory.Amount 100
	Ammo.BackpackAmount 100
	Scale 0.65
	Tag "$TAG_BULLET2"
	Inventory.PickupSound "bulletbox/get"
	Inventory.PickupMessage "$GOTBULLET2"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, "Spawn2")
		CHGP A -1 
		Stop
	Spawn2:
		CHGP B -1
		Stop
	}
}