Actor ExcessiveForceToken : Inventory {}

actor ExcessiveForce : Weapon
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
    Weapon.SelectionOrder 100
    Weapon.AmmoGive 25
    Weapon.AmmoType "NewCell"
	Weapon.AmmoUse 1
	Obituary "$OB_EXCESS"
	Tag "$TAG_EXCESS"
    Inventory.Pickupmessage "$GOTEXCESS"
    Weapon.upsound "duke/weaponup"
    Scale 0.75
	+WEAPON.NOALERT
	Inventory.Icon "NCHGZ0Y0"
	Weapon.SelectionOrder 100
    States
    {
    Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128, "Spawn2")
        NCHG Z -1
        Stop
    Spawn2:
		NCHG Y -1
		Stop
	Ready:
		TNT1 A 0 A_JumpIfInventory("ExcessiveForceToken",1,"FirstTime")
		NCHG A 1 A_WeaponReady
		Loop
	FirstTime:
		TNT1 A 0 A_TakeInventory("ExcessiveForceToken",0)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("PVPCheck",0,0,0,0) == 2,"Ready")
		TNT1 A 0 A_JumpIf(CallACS("PickupAnimation")==0, "Ready")
		NCHG A 2 A_WeaponReady
		TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
		XFFS ABCD 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS E 5 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS E 0 A_PlaySound("plasma/fiddle1")
		XFFS EFGFEEFGF 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS E 5 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		TNT1 A 0 A_PlaySound("weapon/fiddle", CHAN_AUTO)
		XFFS DCBA 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		NCHG A 7 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		NCHG A 0 A_PlaySound("plasma/fiddle2")
		XFFS HI 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS J 0 A_SetPitch(pitch+1)
		XFFS J 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS K 0 A_SetPitch(pitch-1)
		XFFS K 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS L 0 A_PlaySound("plasma/fiddle3", CHAN_AUTO)
		XFFS LMNOPQRSTU 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS K 4 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		XFFS J 0 A_PlaySound("plasma/fiddle4")
		XFFS JIH 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		Goto Ready
	Select:
		NCHG A 1 A_Raise
		NCHG A 0 A_Raise
		Loop
	Deselect:
		NCHG A 1 A_Lower
		NCHG A 0 A_Lower
		Loop
	Fire:
		NCHG A 0 A_AlertMonsters
		NCHG A 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
		NCHG A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
		Goto ContinueFire
	QuadFire:
		TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
		NCHG A 0 A_PlaySound("QuadDamage/Use")
		Goto ContinueFire
	DoubleFire:
		TNT1 A 0 A_SetBlend("Red", 0.25, 16)
		NCHG A 0 A_PlaySound("DoubleDamage/Use")
	ContinueFire:
		NCHG A 0 A_PlayWeaponSound ("plasma/fire")
		NCHG B 0 A_Light1
		NCHG B 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	    NCHG B 0 A_SetPitch(pitch-1)
		NCHG B 0 A_FireCustomMissile("PlasmaCannonProjectile",-5.625)
		NCHG B 0 A_FireCustomMissile("PlasmaCannonProjectile",5.625)
		NCHG B 0 A_FireCustomMissile("PlasmaCannonProjectile")
		NCHG C 0 A_Light0
		NCHG C 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		NCHG D 0 A_SetPitch(pitch+0.50)
		NCHG D 1 Bright A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		NCHG E 0 A_SetPitch(pitch+0.50)
		NCHG E 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
		NCHG A 1 A_WeaponReady
		Goto Ready
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire)
	TNT1 A 0 A_ReFire
	Goto Ready
	WeaponStill:
		NCHG A 22
		Goto LightDone
	}
}

Actor PlasmaCannonProjectile
{
  +FORCEXYBILLBOARD
  Mass 1
  Radius 8
  Height 6
  Speed 20
  Damage 8
  translation "112:127=198:207"
  Scale 0.65
  DeathSound "q2soldier/shotx"
  DamageType "Electric"
  PROJECTILE
  RenderStyle Add
  States
  {
  Spawn:
	ACID A 1 Bright
	Loop
  Death:
	ACID BCDEF 1 Bright
	Stop
  }
}