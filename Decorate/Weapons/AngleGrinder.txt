Actor AngleGrinderToken : Inventory {}

Actor AngleGrinder : Chainsaw
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.UpSound "grinder/select"
	Weapon.ReadySound ""
	Tag "$TAG_GRINDER"
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTGRINDER"
	Obituary "$OB_GRINDER"
	Inventory.Icon "CSAWA0B0"
	Weapon.SelectionOrder 2200
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 0 A_Jump(128, "Spawn2")
	CSAW A -1 
	Stop
	Spawn2:
	CSAW B -1
	Stop
	Ready:
	TNT1 A 0 A_JumpIfInventory("AngleGrinderToken",1,"FirstTime")
    SAWG A 1 A_WeaponReady
	Loop
	FirstTime:
	TNT1 A 0 A_TakeInventory("AngleGrinderToken",0)
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("PVPCheck",0,0,0,0) == 2,"Ready")
	TNT1 A 0 A_JumpIf(CallACS("PickupAnimation")==0, "Ready")
	SAWG A 2 A_WeaponReady
	TNT1 A 0 A_PlaySound("weapon/fiddle")
	AGFS ABCDEEEEEEE 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_PlaySound("weapon/fiddle")
	AGFS FGHHHIJ 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_SetBlend ("FF0000",0.05,5)
	TNT1 A 0 A_PlaySound("pp11",CHAN_VOICE,0.35)
	AGFS KLLLLL 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_PlaySound("weapon/fiddle")
	AGFS MNEEEE 3 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_PlaySound("weapon/fiddle")
	AGFS DCBA 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	Goto Ready
	Fire:
	SAWG A 0 A_AlertMonsters
	SAWG A 0 A_PlaySound("grinder/start", CHAN_WEAPON)
	SAWG ACD 2 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	SAWG E 0 A_PlaySound("grinder/continue", CHAN_WEAPON, 1.0, true)
	CheckForQuad:
	SAWG E 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire")
	SAWG E 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire")
	Goto ContinueFire
	QuadFire:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	SAWG E 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire
	DoubleFire:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	SAWG E 0 A_PlaySound("DoubleDamage/Use")
	ContinueFire:
	SAWG E 0 A_AlertMonsters
	SAWG E 1 A_CustomPunch(2*random(1,10),1,0,"GrinderPuff")
	SAWG F 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	SAWG G 1 A_CustomPunch(2*random(1,10),1,0,"GrinderPuff")
	SAWG H 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 0 A_Refire("CheckForQuad")
	SAWG D 0 A_StopSound(CHAN_WEAPON)
	SAWG D 0 A_PlaySound("grinder/stop", CHAN_WEAPON)
	SAWG DCBABBAABBBAAABBBBAAAABBBBBAAAAA 2 A_WeaponReady
	goto Ready
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire)
	TNT1 A 0 A_ReFire
	Goto Ready
	WeaponStill:
	SAWG A 22
	Goto LightDone
  Deselect:
	SAWG A 0 A_StopSound(CHAN_WEAPON) //This is to prevent the Angle Grinder from continuing to play its attack sound if the player dies
    SAWG A 1 A_Lower
    SAWG A 0 A_Lower
    Loop
  Select:
    SAWG A 1 A_Raise
    SAWG A 0 A_Raise
    Loop
	}
}

ACTOR GrinderPuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +PUFFONACTORS
  activesound ""
  attacksound "grinder/hitwall"
  seesound "grinder/hitbody"
  DamageType "AngleGrinder"
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1
    stop
  Crash:
     TNT1 A 0
     TNT1 A 1
     stop
  Melee:
     TNT1 A 0
     TNT1 A 1
     stop
  }
}