Actor GMIDHandGrenadeCharge : Inventory
{
  Inventory.MaxAmount 36
}

Actor GMIDHandGrenade : Weapon replaces ArtiTimeBomb
{
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.0
	Weapon.AmmoType1 "GMIDHandGrenadeAmmo"
	Weapon.AmmoGive 1
	Weapon.AmmoUse 1
	Inventory.PickupSound "Grenade/Pickup"
	Weapon.UpSound "Grenade/Select"
	Tag "$TAG_GRENADE"
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTGRENADE"
	Obituary "$OB_GRENADE"
	Inventory.Icon "HGRPA0"
	Weapon.SelectionOrder 2800
	Scale 0.6
	States
	{
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3", "Spawn4")
  Spawn1:
	HGRP A -1
	Stop
  Spawn2:
	HGRP B -1
	Stop
  Spawn3:
	HGRP C -1
	Stop
  Spawn4:
	HGRP D -1
	Stop
  Deselect:
	FCH2 K 0 A_JumpIfInventory("GMIDHandGrenadeAmmo",1,"DeselectWithAmmo")
	FCH2 K 1 A_Lower
	FCH2 K 0 A_Lower
	Loop
  DeselectWithAmmo:
    HGRN A 1 A_Lower
    HGRN A 0 A_Lower
    Loop
  Select:
	FCH2 K 0 A_JumpIfInventory("GMIDHandGrenadeAmmo",1,"SelectWithAmmo")
	FCH2 K 1 A_Raise
	FCH2 K 0 A_Raise
	Loop
  SelectWithAmmo:
    HGRN A 1 A_Raise
    HGRN A 0 A_Raise
    Loop
  Ready:
	FCH2 K 0 A_JumpIfInventory("GMIDHandGrenadeAmmo",1,"ReadyWithAmmo")
	FCH2 K 1 A_WeaponReady
	Loop
  ReadyWithAmmo:
	HGRN A 1 A_WeaponReady
	Loop
  Fire:
	HGRN B 0 A_PlaySound("Grenade/Pull",CHAN_WEAPON)
	HGRN BCDEFGHIIIJKLMNNN 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	HGRN N 1 Offset(0,35)
	HGRN N 1 Offset(0,40)
	HGRN N 1 Offset(0,50)
	HGRN N 1 Offset(0,60)
	HGRN N 1 Offset(0,70)
	HGRN N 1 Offset(0,80)
	HGRN N 1 Offset(0,90)
	HGRN N 1 Offset(0,100)
	HGRN N 1 Offset(0,110)
	HGRN N 0 Offset(0,32)
	THRG A 0 A_ReFire("ThrowEnergy1")
	Goto ActuallyThrow1
  ThrowEnergy1:
	THRG A 1 Bright A_JumpIfInventory("GMIDHandGrenadeCharge",12,"ThrowEnergy2")
	THRG A 0 A_GiveInventory("GMIDHandGrenadeCharge",1)
	THRG A 0 A_ReFire("ThrowEnergy1")
	Goto ActuallyThrow1
  ThrowEnergy2:
	THRG B 1 Bright A_JumpIfInventory("GMIDHandGrenadeCharge",24,"ThrowEnergy3")
	THRG B 0 A_GiveInventory("GMIDHandGrenadeCharge",1)
	THRG B 0 A_ReFire("ThrowEnergy2")
	Goto ActuallyThrow2
  ThrowEnergy3:
	THRG C 1 Bright A_JumpIfInventory("GMIDHandGrenadeCharge",36,"ThrowEnergy4")
	THRG C 0 A_GiveInventory("GMIDHandGrenadeCharge",1)
	THRG C 0 A_ReFire("ThrowEnergy3")
	Goto ActuallyThrow3
  ThrowEnergy4:
	THRG D 1 Bright
	THRG D 0 A_ReFire("ThrowEnergy4")
	Goto ActuallyThrow4
  ActuallyThrow1:
	THR1 A 0 A_TakeInventory("GMIDHandGrenadeCharge",0)
	THR1 A 0 A_PlaySound("Grenade/Toss",CHAN_WEAPON)
	THR1 ABCD 1
	THR1 E 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire1")
	THR1 A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire1")
	Goto ContinueFire1
  QuadFire1:
    TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	THR1 E 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire1
  DoubleFire1:
    TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	THR1 E 0 A_PlaySound("DoubleDamage/Use")
  ContinueFire1:
	THR1 E 1 A_FireCustomMissile("ThrownHandGrenade1",0,1,0,0)
	THR1 FGHIII 1
	THR1 I 1 Offset(0,35)
	THR1 I 1 Offset(0,40)
	THR1 J 1 Offset(0,50)
	THR1 J 1 Offset(0,60)
	THR1 J 1 Offset(0,70)
	THR1 J 1 Offset(0,80)
	THR1 J 1 Offset(0,90)
	THR1 J 1 Offset(0,100)
	THR1 J 1 Offset(0,110)
	Goto Recharge
  ActuallyThrow2:
	THR1 A 0 A_TakeInventory("GMIDHandGrenadeCharge",0)
	THR1 A 0 A_PlaySound("Grenade/Toss",CHAN_WEAPON)
	THR1 AC 1
	THR1 E 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire2")
	THR1 A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire2")
	Goto ContinueFire2
  QuadFire2:
    TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	THR1 E 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire2
  DoubleFire2:
    TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	THR1 E 0 A_PlaySound("DoubleDamage/Use")
  ContinueFire2:
	THR1 E 1 A_FireCustomMissile("ThrownHandGrenade2",0,1,0,0)
	THR1 GIII 1
	THR1 I 1 Offset(0,35)
	THR1 I 1 Offset(0,40)
	THR1 J 1 Offset(0,50)
	THR1 J 1 Offset(0,60)
	THR1 J 1 Offset(0,70)
	THR1 J 1 Offset(0,80)
	THR1 J 1 Offset(0,90)
	THR1 J 1 Offset(0,100)
	THR1 J 1 Offset(0,110)
	TNT1 A 4
	Goto Recharge
  ActuallyThrow3:
	THR2 A 0 A_TakeInventory("GMIDHandGrenadeCharge",0)
	THR2 A 0 A_PlaySound("Grenade/Toss",CHAN_WEAPON)
	THR2 ABC 1
	THR2 D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire3")
	THR2 A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire3")
	Goto ContinueFire3
  QuadFire3:
    TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	THR2 D 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire3
  DoubleFire3:
    TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	THR2 D 0 A_PlaySound("DoubleDamage/Use")
  ContinueFire3:
	THR2 D 1 A_FireCustomMissile("ThrownHandGrenade3",0,1,0,0)
	THR2 EFGHI 1
	TNT1 A 9
	Goto Recharge
  ActuallyThrow4:
	THR2 A 0 A_TakeInventory("GMIDHandGrenadeCharge",0)
	THR2 A 0 A_PlaySound("Grenade/Toss",CHAN_WEAPON)
	THR2 AC 1
	THR2 D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadFire4")
	THR2 A 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleFire4")
	Goto ContinueFire4
  QuadFire4:
    TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	THR2 D 0 A_PlaySound("QuadDamage/Use")
	Goto ContinueFire4
  DoubleFire4:
    TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	THR2 D 0 A_PlaySound("DoubleDamage/Use")
  ContinueFire4:
	THR2 D 1 A_FireCustomMissile("ThrownHandGrenade4",0,1,0,0)
	THR2 FGI 1
	TNT1 A 12
	Goto Recharge
  Recharge:
	FCH2 K 0 A_JumpIfInventory("GMIDHandGrenadeAmmo",1,"RechargeWithAmmo")
	FCH2 K 1 Offset(0,110)
	FCH2 K 1 Offset(0,100)
	FCH2 K 1 Offset(0,90)
	FCH2 K 1 Offset(0,80)
	FCH2 K 1 Offset(0,70)
	FCH2 K 1 Offset(0,60)
	FCH2 K 1 Offset(0,50)
	FCH2 K 1 Offset(0,40)
	FCH2 K 1 Offset(0,35)
	Goto Ready
  RechargeWithAmmo:
	HGRN A 1 Offset(0,110)
	HGRN A 1 Offset(0,100)
	HGRN A 1 Offset(0,90)
	HGRN A 1 Offset(0,80)
	HGRN A 1 Offset(0,70)
	HGRN A 1 Offset(0,60)
	HGRN A 1 Offset(0,50)
	HGRN A 1 Offset(0,40)
	HGRN A 1 Offset(0,35)
	Goto Ready
  AltFire:
	TNT1 A 0 A_GunFlash("WeaponStill")
	TNT1 A 0 A_PlaySound ("weapons/KICK",CHAN_WEAPON)
	FOOT AB 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT C 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_JumpIfInventory("PowerQuadDamageST", 1, "QuadKick")
	FOOT D 0 A_JumpIfInventory("PowerDoubleDamage", 1, "DoubleKick")
	Goto ContinueKick
  QuadKick:
	TNT1 A 0 A_SetBlend("Blue", 0.25, 16)
	FOOT D 0 A_PlaySound("QuadDamage/Use",CHAN_7)
	Goto ContinueKick
  DoubleKick:
	TNT1 A 0 A_SetBlend("Red", 0.25, 16)
	FOOT D 0 A_PlaySound("DoubleDamage/Use",CHAN_7)
  ContinueKick:
	FOOT D 0 A_CustomPunch(5,0,0,"KickPuff")
	FOOT D 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT D 0 A_Quake(1,2,0,10,"")
	FOOT EF 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	FOOT FEDCBA 1 A_WeaponReady(WRF_NoFire|WRF_NoSwitch)
	TNT1 A 8 A_WeaponReady(WRF_NoFire)
	TNT1 A 0 A_ReFire
	Goto Ready
  WeaponStill:
	FCH2 K 0 A_JumpIfInventory("GMIDHandGrenadeAmmo",1,"WeaponStillWithAmmo")
	FCH2 K 22
	Goto LightDone
  WeaponStillWithAmmo:
	HGRN A 22
	Goto LightDone
	}
}

ACTOR GMIDHandGrenadeAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 15
	Ammo.BackpackAmount 3
	Ammo.BackpackMaxAmount 30
	Inventory.PickupMessage "The weapon IS the ammo, genius!"
	Inventory.Icon HGRPA0
   States
   {
   Spawn:
      NULL A -1
      Loop
   }
}

Actor ThrownHandGrenade1
{
	Radius 12
	Height 15
	Mass 9
	Damage 10
	Speed 8
	Scale 0.6
	BounceType "Doom"
	BounceCount 10
	ReactionTime 23
	PROJECTILE
	-NOGRAVITY
	+CANBOUNCEWATER
	BounceSound "Grenade/Bounce"
	WallBounceSound "Grenade/Bounce"
	States
	{
	Spawn:
	  HGRT A 0 A_SpawnItemEx("HandGrenadeSmokeTrail",0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
	  HGRT A 1 A_Countdown
	  HGRT BCDEFGH 1 A_SpawnItemEx("HandGrenadeSmokeTrail",0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0),Random(1,360),128)
	  Loop
	Death:
	  ZXPL A 0 A_AlertMonsters
	  ZXPL A 0 A_PlaySound("explosion/distant", CHAN_VOICE)
	  ZXPL A 0 A_PlaySound("newexplosion")
	  BEXP D 0 A_SpawnItem("ExplosionSpawner")
	  Stop
	}
}

Actor ThrownHandGrenade2 : ThrownHandGrenade1
{
	Speed 16
}

Actor ThrownHandGrenade3 : ThrownHandGrenade1
{
	Speed 24
}

Actor ThrownHandGrenade4 : ThrownHandGrenade1
{
	Speed 30
}

Actor HandGrenadeSmokeTrail : CasingSmokeTrail
{
	RenderStyle Translucent
}