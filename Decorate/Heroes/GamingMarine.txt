ACTOR StartingAnimation : Inventory {} //When you start a new game, the weapon the player first pulls out (The beretta) will check to see if the player has this in their inventory
									   //(Which they should).
									   //If they do, then the check passes, and a Duke 3D-esque starting animation will play.
									   //Otherwise, the player will pull out the weapon normally.
									   //Base code from Aracnocide.

Actor GamingMarine : DoomPlayer
{
	Player.DisplayName "GamingMarine"
	Player.StartItem "GMIDPistol"
	Player.StartItem "GMIDFist"
	Player.StartItem "GMIDHandGrenade"
	Player.StartItem "Clip2", 75
	Player.StartItem "GMIDHandGrenadeAmmo", 3
	Player.StartItem "StartingAnimation", 1
	Player.StartItem "AngleGrinderToken", 1
	Player.StartItem "DualPistolToken", 1
	Player.StartItem "GMIDShotgunToken", 1
	Player.StartItem "FirestickToken", 1
	Player.StartItem "SawnOffToken", 1
	Player.StartItem "VGunToken", 1
	Player.StartItem "RevXToken", 1
	Player.StartItem "GMIDMissileLauncherToken", 1
	Player.StartItem "PineappleJammerToken", 1
	Player.StartItem "RailgunToken", 1
	Player.StartItem "ExcessiveForceToken", 1
	Player.StartItem "IsPlayer", 1
	Player.SoundClass "GamingMarine"
	Player.Face "GMN"
	DamageFactor "Cube", 0
	DamageFactor "CookieElectric", 0
	DamageFactor "CookieFire", 0
	DamageFactor "CookieIce", 0
	DamageFactor "Sentry", 0
	Player.JumpZ 10
	Player.WeaponSlot 1, GMIDFist, AngleGrinder
	Player.WeaponSlot 2, GMIDPistol, GMIDDualPistol
	Player.WeaponSlot 3, GMIDShotgun, Firestick, GMIDSuperShotgun
	Player.WeaponSlot 4, VGun, RevX
	Player.WeaponSlot 5, GMIDMissileLauncher, PineappleJammer
	Player.WeaponSlot 6, GMIDRailgun, ExcessiveForce
	Player.WeaponSlot 7, SpaceCadet
	Player.WeaponSlot 8, SoulCube
	Player.WeaponSlot 9, GMIDHandGrenade
	Player.CrouchSprite "GMRC"
	States
	{
	Spawn:
		GMRN Z -1
		Loop
	See:
		GMRN ABCD 6
		Loop
	Missile:
		GMRN E 12
		Goto Spawn
	Melee:
		GMRN F 6 Bright
		Goto Missile
	Pain:
		GMRN G 4 
		GMRN G 4 A_Pain
		Goto Spawn
	Pain.Drowning:
		GMRN G 4
		GMRN G 4 A_PlaySound("player/gurgle", CHAN_VOICE)
		Goto Spawn
	Pain.Falling:
		GMRN G 1
		GMRN G 7 A_PlaySound("player/falldamage", CHAN_VOICE)
		Goto Spawn
	Death:
		TNT1 A 0 A_Jump(256, "NewDeath1", "NewDeath2")
	NewDeath1:
		GMRN H 5
		GMRN I 5 A_PlayerScream
		GMRN J 5 A_NoBlocking
		GMRN K 5
		GMRN L 0 A_PlaySound("body/hitfloor")
		GMRN LM 5
		GMRN N -1
		Stop
	NewDeath2:
		PDTH A 5
		PDTH B 5 A_PlayerScream
		PDTH C 5 A_NoBlocking
		PDTH D 5
		PDTH E 0 A_PlaySound("body/hitfloor")
		PDTH E -1
		Stop
	Death.Drowning:
		GMRN H 5
		GMRN I 5 A_PlaySound("player/drown", CHAN_VOICE)
		GMRN J 5 A_NoBlocking
		GMRN KLM 5
		GMRN N -1
		Stop
	Death.Fire:
		PBRN A 0 Bright A_PlaySound("player/imonfire", CHAN_VOICE) //I would put A_ItBurnsItBurns here, but DoomPlayer doesn't inherit from Strife actors.
		PBRN A 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN A 5 Bright
		PBRN B 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN B 5 Bright
		PBRN C 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN C 5 Bright
		PBRN D 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN D 5 Bright A_NoBlocking
		PBRN E 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN E 5 Bright
		PBRN F 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN F 5 Bright
		PBRN G 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN G 5 Bright
		PBRN H 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN H 5 Bright
		PBRN I 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN I 5 Bright
		PBRN J 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN J 5 Bright
		PBRN K 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN K 5 Bright
		PBRN L 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN L 5 Bright
		PBRN M 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN M 5 Bright
		PBRN N 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN N 5 Bright //A_CrispyPlayer //I can't get this to work, unfortunately. It would be cool to view the player's arms waving while on fire ala Strife, though. :( 
		PBRN O 0 A_SpawnItem("BFireTrail",8+random(-5,5),58+random(-5,5),0,0)
		PBRN OPQ 5 Bright
		PBRN RSTU 7 Bright
		PBRN V -1
		Stop
	Death.Melee:
		PLA2 H 5
		PLA2 I 5 A_PlaySound("player/meleedie", CHAN_VOICE)
		PLA2 J 5 A_NoBlocking
		PLA2 KLM 5
		PLA2 N 0 A_PlaySound("body/hitfloor")
		PLA2 NO 5
		PLA2 P -1
		Stop
	Death.Electric:
		PDTH A 0 A_PlaySound("player/zaps", CHAN_VOICE)
		PZAP AABBAABBAABBAABBAABBAABBAA 1 bright A_SpawnItemEx("expsmoke",0,random(-0,0),0,random(-1,1),random(-1,1),random(1,3),random(0,0)	)
		PDTH A 5
		PDTH B 5 A_PlayerScream
		PDTH C 5 A_NoBlocking
		PDTH D 5
		PDTH E 0 A_PlaySound("body/hitfloor")
		PDTH E -1
		Stop
	Death.Falling:
		GMRN H 5 A_PlaySound("player/falldamage", CHAN_VOICE)
		GMRN I 5
		GMRN J 5 A_NoBlocking
		GMRN K 5
		GMRN L 0 A_PlaySound("body/hitfloor")
		GMRN LM 5
		GMRN N -1
		Stop
	XDeath:
	Death.ForceLord:
	Death.GMIDExplosion:
	Death.GMIDRailgun:
		TNT1 A 0 A_StopSound(CHAN_VOICE)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItem("GMIDGibSpawner")
		TNT1 A -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("body/crush",CHAN_VOICE)
		POL5 A -1
		Stop
	}
}

//Player taunting code
//Based on Duke Nukem: War Against the Doomed by xenoxols
Actor PlayerTauntCooldown : Inventory
{
	Inventory.MaxAmount 5
	Inventory.InterHubAmount 0
	+UNDROPPABLE
	-INVBAR
}

Actor PlayerTaunt : CustomInventory
{
	Inventory.MaxAmount 0
	+AUTOACTIVATE
	States
	{
	Spawn:
	UseStop:
		TNT1 A 0
		stop
	}
}

Actor PlayerKillTaunt : PlayerTaunt
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIf(CallACS("PlayerVocalizations")==true, "Pickup2")
		stop
	  Pickup2:
		TNT1 A 0 A_JumpIfInventory("PlayerTauntCooldown",1,"UseStop")
        TNT1 A 0 A_PlaySound("player/killtaunt",CHAN_VOICE,2.0,false,ATTN_NONE)
        TNT1 A 0 A_GiveInventory("PlayerTauntCooldown",5)
        TNT1 A 0 ACS_NamedExecuteAlways("PlayerTauntCooldown")
        stop
	}
}

Actor PlayerKickTaunt : PlayerTaunt
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIf(CallACS("PlayerVocalizations")==true, "Pickup2")
		stop
	  Pickup2:
		TNT1 A 40
		TNT1 A 0 A_JumpIfInventory("PlayerTauntCooldown",1,"UseStop")
        TNT1 A 0 A_PlaySound("player/kicktaunt",CHAN_VOICE,2.0,false,ATTN_NONE)
        TNT1 A 0 A_GiveInventory("PlayerTauntCooldown",5)
        TNT1 A 0 ACS_NamedExecuteAlways("PlayerTauntCooldown")
        stop
	}
}

Actor PlayerHeadshotTaunt : PlayerTaunt
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIf(CallACS("PlayerVocalizations")==true, "Pickup2")
		stop
	  Pickup2:
		TNT1 A 40
		TNT1 A 0 A_JumpIfInventory("PlayerTauntCooldown",1,"UseStop")
        TNT1 A 0 A_PlaySound("player/headshot",CHAN_VOICE,2.0,false,ATTN_NONE)
        TNT1 A 0 A_GiveInventory("PlayerTauntCooldown",5)
        TNT1 A 0 ACS_NamedExecuteAlways("PlayerTauntCooldown")
        stop
	}
}

Actor PlayerGibTaunt : PlayerTaunt
{
	States
	{
	  Pickup:
		TNT1 A 0 A_JumpIf(CallACS("PlayerVocalizations")==true, "Pickup2")
		stop
	  Pickup2:
		TNT1 A 40
		TNT1 A 0 A_JumpIfInventory("PlayerTauntCooldown",1,"UseStop")
        TNT1 A 0 A_PlaySound("player/gibtaunt",CHAN_VOICE,2.0,false,ATTN_NONE)
        TNT1 A 0 A_GiveInventory("PlayerTauntCooldown",5)
        TNT1 A 0 ACS_NamedExecuteAlways("PlayerTauntCooldown")
        stop
	}
}