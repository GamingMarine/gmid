ACTOR ZombieScientist
{
  obituary "%o was axed by a Zombie Scientist."
  health 20
  mass 90
  speed 4
  Radius 20
  Height 52
  painchance 200
  PainChance "Fire", 256
  seesound "scientist/sight"
  painsound "scientist/sight"
  deathsound "scientist/death"
  activesound "scientist/sight"
  MONSTER
  States
  {
   Spawn:
    SCZA A 10 A_Look
    loop
  See:
    SCZA AAAABBBB 2 A_Chase
	SCZA C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)
 	SCZA CCCCDDDD 2 A_Chase
	SCZA A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)
    loop
  Melee:
	SCZA E 0 A_PlaySound("scientist/attack", 2)
    SCZA E 8 A_FaceTarget
    SCZA F 16 A_CustomMeleeAttack (random (2, 6) *5, "scientist/hit", "scientist/whoosh")
    SCZA E 4
    goto See
  Pain:
    SCZA G 3
    SCZA G 3 A_Pain
    goto See
  Death:
	TNT1 A 0 A_Jump(256, "Death1", "Death2")
  Death1:
    SCZA H 5
    SCZA I 5 A_Scream
    SCZA J 5 A_NoBlocking
    SCZA K 5
    SCZA L 5
    SCZA M 5
	SCZA N 10
	SCZA N 0 A_Jump(32, "KillTaunt1")
    SCZA N -1
    stop
  Death2:
    FSMS H 5
    FSMS I 5 A_Scream
    FSMS J 5 A_NoBlocking
    FSMS K 5
	FSMS L 10
	FSMS L 0 A_Jump(32, "KillTaunt2")
    FSMS L -1
    stop
  KillTaunt1:
	TNT1 A 0 A_PlaySound("player/killtaunt", CHAN_AUTO)
    SCZA N -1
    stop
  KillTaunt2:
	TNT1 A 0 A_PlaySound("player/killtaunt", CHAN_AUTO)
    FSMS L -1
    stop
  XDeath:
	TNT1 A 0 A_XScream
	TNT1 A 0 A_NoBlocking
	TNT1 A 0 A_SpawnDebris("SkullGib")
	TNT1 A 0 A_SpawnDebris("DukeGibs1")
	TNT1 A 0 A_SpawnDebris("DukeGibs2")
	TNT1 A 0 A_SpawnDebris("DukeGibs3")
	TNT1 A 0 A_SpawnDebris("GutsGib")
	TNT1 A 0 A_SpawnDebris("DukeGibs5")
	TNT1 A 0 A_SpawnDebris("BloodSplurt")
	TNT1 A 0 A_Jump(16, "GibTaunt")
	Stop
  GibTaunt:
	TNT1 A 0 A_PlaySound("player/gibtaunt", CHAN_AUTO)
	Stop
  Raise:
    SCZA MLKJIH 5
    goto See
  Death.Fire:
	ZSC1 G 0 A_NoBlocking
	ZSC1 G 0 A_SpawnItem("BurningBoi")
	TNT1 A -1
	Stop
  }
}