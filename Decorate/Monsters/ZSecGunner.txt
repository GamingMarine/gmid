Actor ZSecGunner
{
  obituary "$OB_ZSEC1"
  health 40
  radius 20
  height 56
  mass 100
  speed 4
  painchance 200
  seesound "grunt/sight"
  painsound "grunt/pain"
  deathsound "grunt/death"
  activesound "grunt/active"
  DropItem "GMIDDualPistol"
  PainChance "Foot", 256
  MONSTER
  +FLOORCLIP
  +MISSILEMORE
  +DONTHURTSPECIES
  Tag "$TAG_ZSEC1"
  states
  {
  Spawn:
    ZSC1 AAAAA 10 A_Look
    ZSC1 A 0 A_Jump(8, 2)
    ZSC1 A 0
    Loop
    ZSC1 A 0 A_PlaySound("ZSec/Idle")
    Loop
  See:
    ZSC1 AAAABBBB 1 A_Chase
	ZSC1 C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	ZSC1 CCCCDDDD 1 A_Chase
	ZSC1 A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
    Goto See
  Missile:
	ZSC1 E 0 A_PlaySound("grunt/prepare", CHAN_VOICE)
    ZSC1 E 10 A_FaceTarget
    ZSC1 F 0 bright A_PlaySound("ZSec/Pistol")
    ZSC1 F 2 bright A_CustomBulletAttack(3, 3, 1, 3, "XaserPuff_NoTaunt")
    ZSC1 E 10 A_FaceTarget
    ZSC1 E 0 A_Jump(128, 2)
    ZSC1 E 4 A_FaceTarget
    ZSC1 E 0 A_Jump(128, 2)
    ZSC1 E 4 A_FaceTarget
    ZSC1 E 0 A_Jump(128, 2)
    ZSC1 E 4 A_FaceTarget
    ZSC1 E 0 A_Jump(128, 2)
    ZSC1 E 0 A_SpidReFire
    Goto Missile+2
    ZSC1 E 0
    Goto See
  Pain:
    ZSC1 G 3 A_StopSound(CHAN_VOICE)
    ZSC1 G 3 A_Pain
    Goto See
  Pain.Foot:
  Pain.BigFist:
    ZSC1 G 2 A_FaceTarget
    ZSC1 A 0 A_ChangeFlag("NOPAIN",1)
    ZSC1 A 0 A_Recoil(14)
    ZSC1 G 33 A_Pain
	ZSC1 G 0 A_ChangeFlag("NOPAIN",0)
    Goto See
  Pain.Uppercut:
	ZSC1 G 0 A_FaceTarget
	ZSC1 A 0 A_ChangeFlag("NOPAIN",1)
	ZSC1 A 0 ThrustThingZ(0,16,0,0)
	ZSC1 G 33 A_Pain
	ZSC1 G 0 A_ChangeFlag("NOPAIN",0)
	Goto See
  Death:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
    ZSC1 G 5
    ZSC1 H 5 A_Scream
    ZSC1 I 5 A_NoBlocking
    ZSC1 JK 5
	ZSC1 L 0 A_PlaySound("body/hitfloor")
    ZSC1 L -1
	Stop
  Death.Basic:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
    ZSC1 G 5
    ZSC1 H 5 A_Scream
    ZSC1 I 5 A_NoBlocking
    ZSC1 JK 5
	ZSC1 L 10 A_PlaySound("body/hitfloor")
	TNT1 A 0 A_Jump(16, "KillTaunt")
    ZSC1 L -1
	Stop
  KillTaunt:
  	TNT1 A 0 A_PlaySound("player/killtaunt", CHAN_AUTO)
    ZSC1 L -1
	Stop
  Death.Foot:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
    ZSC1 G 5 ThrustThingZ(0,16,0,0)
    ZSC1 H 5 A_Scream
    ZSC1 I 5 A_NoBlocking
    ZSC1 JK 5
	ZSC1 L 10 A_PlaySound("body/hitfloor")
	TNT1 A 0 A_Jump(16, "KickTaunt")
    ZSC1 L -1
	Stop
  KickTaunt:
  	TNT1 A 0 A_PlaySound("player/kicktaunt", CHAN_AUTO)
    ZSC1 L -1
	Stop
  Death.BigFist:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
    ZSC1 G 2 A_FaceTarget
	ZSC1 G 0 A_Recoil(14)
	ZSC1 G 3
    ZSC1 H 5 A_Scream
    ZSC1 I 5 A_NoBlocking
    ZSC1 JK 5
	ZSC1 L 10 A_PlaySound("body/hitfloor")
	TNT1 A 0 A_Jump(16, "KillTaunt")
    ZSC1 L -1
	Stop
  Death.Uppercut:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
	ZSC1 G 0 A_FaceTarget
	ZSC1 G 0 ThrustThingZ(0,43,0,0)
	ZSC1 G 5
    ZSC1 H 5 A_Scream
    ZSC1 I 5 A_NoBlocking
    ZSC1 JK 5
	ZSC1 L 10 A_PlaySound("body/hitfloor")
	TNT1 A 0 A_Jump(16, "KillTaunt")
    ZSC1 L -1
	Stop
  XDeath:
  Death.GMIDExplosion:
  Death.GMIDRailgun:
	TNT1 A 0 A_GiveToTarget("ScoreItem",200)
	TNT1 A 0 A_XScream
	TNT1 A 0 A_NoBlocking
	TNT1 A 0 A_SpawnItem("GMIDGibSpawner")
	TNT1 A 0 A_Jump(16, "GibTaunt")
	Stop
  GibTaunt:
	TNT1 A 40
	TNT1 A 0 A_PlaySound("player/gibtaunt", CHAN_AUTO)
	Stop
  Death.Fire:
  Death.CookieFire:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",200)
	ZSC1 G 0 A_NoBlocking
	ZSC1 G 0 A_SpawnItem("BurningBoi")
	TNT1 A -1
	Stop
  Death.CookieIce:
	"####" "#" 5 A_GenericFreezeDeath
	"----" A 1 A_FreezeDeathChunks
	Wait
  Crush:
	TNT1 A 0 A_PlaySound("body/crush",CHAN_VOICE)
	POL5 A -1
	Stop
  }
}