ACTOR BlasterSoldier 
{
  Health 40
  Radius 20
  Height 56
  Speed 4
  PainChance 175
  Monster
  +FLOORCLIP
  SeeSound "BlasterSoldier/Sight"
  ActiveSound "BlasterSoldier/Radio"
  PainSound "BlasterSoldier/Pain"
  DeathSound "BlasterSoldier/Death"
  Obituary "$OB_BLASTER"
  DropItem "ClipSpawner"
  Tag "$TAG_BLASTER"
  States
  {
  Spawn:
    POFD A 10 A_Look
    Loop
  See:
    POFD AAAABBBB 1 A_Chase
	POFD C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	POFD CCCCDDDD 1 A_Chase
	POFD A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
    Loop
  Missile:
    POFD E 4 A_FaceTarget
	POFD E 0 A_PlaySound("Q2Soldier/Blaster")
    POFD F 11 BRIGHT A_CustomMissile("Q2SoldierProjectile",35,8,0)
    POFD E 3 A_FaceTarget
	POFD E 0 A_PlaySound("Q2Soldier/Blaster")
    POFD F 8 BRIGHT A_CustomMissile("Q2SoldierProjectile",35,8,0)
    POFD E 8
    Goto See
  Pain:
    POFD G 3
    POFD G 3 A_Pain
    Goto See
  Death:
    POFD H 5 A_Scream
    POFD I 5
    POFD J 5 A_NoBlocking
    POFD K 5
	POFD L 0 A_PlaySound("body/hitfloor")
	POFD L 10
	TNT1 A 0 A_Jump(16, "KillTaunt")
    POFD L -1
    Stop
  KillTaunt:
	TNT1 A 0 A_PlaySound("player/killtaunt", CHAN_AUTO)
    POFD L -1
    Stop
  XDeath:
	TNT1 A 0 A_XScream
	TNT1 A 0 A_NoBlocking
	TNT1 A 0 A_SpawnItem("GMIDGibSpawner")
	TNT1 A 0 A_Jump(16, "GibTaunt")
	Stop
  GibTaunt:
	TNT1 A 40
	TNT1 A 0 A_PlaySound("player/gibtaunt", CHAN_AUTO)
	Stop
  Raise:
    POFD K 5
    POFD JIH 5
    Goto See
  Death.Fire:
	ZSC1 G 0 A_NoBlocking
	ZSC1 G 0 A_SpawnItem("BurningBoi")
	TNT1 A -1
	Stop
  }
}