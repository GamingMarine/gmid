actor EvilMarine_Rifle //Evil! EEEVILLL
{
  Game Doom
  health 50
  PainChance 200
  PainChance "Foot", 256
  Speed 7.5
  Radius 16
  Height 56
  Scale 1.06
  Monster
  SeeSound "bgrunt/see"
  PainSound "bgrunt/pain"
  ActiveSound "bgrunt/active"
  DeathSound "bgrunt/death"
  AttackSound "rifle/fire"
  DropItem "UsedClipSpawner"
  DropItem "GMIDHandGrenadeGiver"
  translation "112:127=%[0, 0, 0]:[2, 2, 0]"
  Obituary "$OB_EVILMARINE"
  Tag "$TAG_EVILMARINE1"
  States
  {
  Spawn:
	MARR EH 10 A_Look
    Loop
  See:
	MARR AABB 2 A_FastChase
	MARR C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	MARR CCDD 2 A_FastChase
	MARR A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	Loop
  Pain:
    MARR G 4 A_StopSound(CHAN_VOICE)
	MARR G 4 A_Pain
	Goto See
  Pain.Foot:
  Pain.BigFist:
    MARR G 2 A_FaceTarget
    MARR A 0 A_ChangeFlag("NOPAIN",1)
    MARR A 0 A_Recoil(14)
    MARR G 33 A_Pain
	MARR G 0 A_ChangeFlag("NOPAIN",0)
    Goto See
  Pain.Uppercut:
	MARR G 0 A_FaceTarget
	MARR A 0 A_ChangeFlag("NOPAIN",1)
	MARR A 0 ThrustThingZ(0,16,0,0)
	MARR G 33 A_Pain
	MARR G 0 A_ChangeFlag("NOPAIN",0)
	Goto See
  Missile:
	MARR E 10 A_FaceTarget
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 6
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR E 6
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 2
	MARR F 2 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	MARR E 6
	Goto See
  Death:
  Death.RevX:
	PLA1 G 0 A_GiveToTarget("ScoreItem",250)
	PLA1 G 0 A_Jump(256, "Death1", "Death2", "Death3")
  Death1:
	PLA1 H 5
	PLA1 I 5 A_Scream
	PLA1 J 5 A_NoBlocking
	PLA1 KLM 5
	PLA1 N 10
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	PLA1 N -1
	Stop
  Death2:
	HDSH A 5
	HDSH B 0 A_PlaySound("gore/head", CHAN_VOICE)
	HDSH B 0 A_NoBlocking
	HDSH B 0 A_SpawnItem("GMIDHeadshotSpawner")
	HDSH BC 10
	HDSH B 0 A_GiveToTarget("PlayerHeadshotTaunt",1)
	HDSH BCDE 10
	HDSH F -1
	Stop
  Death3:
	EVMD A 5 A_Scream
	EVMD B 5 A_NoBlocking
	EVMD CD 5
	EVMD E 10
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVMD E -1
	Stop
  Death.AngleGrinder:
	EVC1 A 0 A_GiveToTarget("ScoreItem",250)
	EVC1 A 5 A_Scream
	EVC1 B 0 A_NoBlocking
	EVC1 B 5 A_PlaySound("misc/gibbed",CHAN_AUTO)
	EVC1 C 0 A_FaceTarget
	EVC1 C 5 A_SpawnItemEx("EvilMarineTorso_Rifle",0,0,32,random(-2,-4),random(-1,1),random(2,4),1)
	EVC1 D 5
	EVC1 E 5 A_PlaySound("body/hitfloor")
	EVC1 FG 5
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVC1 G -1
	Stop
  Death.Foot:
	PLA1 G 0 A_GiveToTarget("ScoreItem",250)
	PLA1 G 0 A_Jump(256, "Death.Foot1", "Death.Foot2")
  Death.Foot1:
	PLA1 H 5 ThrustThingZ(0,16,0,0)
	PLA1 I 5 A_Scream
	PLA1 J 5 A_NoBlocking
	PLA1 KLM 5
	PLA1 N 10
	TNT1 A 0 A_GiveToTarget("PlayerKickTaunt",1)
	PLA1 N -1
	Stop
  Death.Foot2:
	EVMD A 0 ThrustThingZ(0,16,0,0)
	EVMD A 5 A_Scream
	EVMD B 5 A_NoBlocking
	EVMD CD 5
	EVMD E 10
	TNT1 A 0 A_GiveToTarget("PlayerKickTaunt",1)
	EVMD E -1
	Stop
  Death.BigFist:
	EVMD A 0 A_GiveToTarget("ScoreItem",250)
	EVMD A 0 A_Scream
	EVMD A 2 A_FaceTarget
	EVMD A 0 A_Recoil(14)
	EVMD A 3
	EVMD B 5 A_NoBlocking
	EVMD CD 5
	EVMD E 10
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVMD E -1
	Stop
  Death.Uppercut:
	EVMD A 0 A_GiveToTarget("ScoreItem",250)
	EVMD A 0 ThrustThingZ(0,43,0,0)
	EVMD A 5 A_Scream
	EVMD B 5 A_NoBlocking
	EVMD CD 5
	EVMD E 10
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVMD E -1
	Stop
  XDeath:
  Death.GMIDExplosion:
  Death.GMIDRailgun:
	TNT1 A 0 A_GiveToTarget("ScoreItem",250)
	TNT1 A 0 A_StopSound(CHAN_VOICE)
	TNT1 A 0 A_XScream
	TNT1 A 0 A_NoBlocking
	TNT1 A 0 A_SpawnItem("GMIDGibSpawner")
	TNT1 A 40
	TNT1 A 0 A_GiveToTarget("PlayerGibTaunt",1)
	Stop
  Death.Fire:
  Death.CookieFire:
	ZSC1 G 0 A_GiveToTarget("ScoreItem",250)
	ZSC1 G 0 A_NoBlocking
	ZSC1 G 0 A_SpawnItem("BurningMarine")
	TNT1 A -1
	Stop
  Death.CookieIce:
	"####" "#" 0 A_StopSound(CHAN_VOICE)
	"####" "#" 5 A_GenericFreezeDeath
	"----" A 1 A_FreezeDeathChunks
	Wait
  Crush:
	TNT1 A 0 A_PlaySound("body/crush",CHAN_VOICE)
	POL5 A -1
	Stop
   }
}

actor EvilMarine_Shotgun : EvilMarine_Rifle
{
  AttackSound "Shotguy/Attack"
  DropItem "GmidShotgun"
  DropItem "GMIDHandGrenadeGiver"
  translation "112:127=%[0, 0, 0]:[2, 1, 0]"
  Tag "$TAG_EVILMARINE2"
  States
  {
  Spawn:
	PLA3 EH 10 A_Look
    Loop
  See:
	PLA3 AABB 2 A_FastChase
	PLA3 C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	PLA3 CCDD 2 A_FastChase
	PLA3 A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	Loop
  Pain:
    PLA3 G 4 A_StopSound(CHAN_VOICE)
	PLA3 G 4 A_Pain
	Goto See
  Pain.Foot:
  Pain.BigFist:
    PLA3 G 2 A_FaceTarget
    PLA3 A 0 A_ChangeFlag("NOPAIN",1)
    PLA3 A 0 A_Recoil(14)
    PLA3 G 33 A_Pain
	PLA3 G 0 A_ChangeFlag("NOPAIN",0)
    Goto See
  Pain.Uppercut:
	PLA3 G 0 A_FaceTarget
	PLA3 A 0 A_ChangeFlag("NOPAIN",1)
	PLA3 A 0 ThrustThingZ(0,16,0,0)
	PLA3 G 33 A_Pain
	PLA3 G 0 A_ChangeFlag("NOPAIN",0)
	Goto See
  Missile:
	PLA3 E 10 A_FaceTarget
	PLA3 F 10 bright A_CustomBulletAttack(22.5, 0, 3, random(1,5)*3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	PLA3 E 10
	Goto See
  Death.AngleGrinder:
	EVC1 A 0 A_GiveToTarget("ScoreItem",250)
	EVC1 A 5 A_Scream
	EVC1 B 0 A_NoBlocking
	EVC1 B 5 A_PlaySound("misc/gibbed",CHAN_AUTO)
	EVC1 C 0 A_FaceTarget
	EVC1 C 5 A_SpawnItemEx("EvilMarineTorso_Shotgun",0,0,32,random(-2,-4),random(-1,1),random(2,4),1)
	EVC1 D 5
	EVC1 E 5 A_PlaySound("body/hitfloor")
	EVC1 FG 5
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVC1 G -1
	Stop
   }
}

actor EvilMarine_Minigun : EvilMarine_Rifle
{
  AttackSound "weapons/UZIFIRE1"
  DropItem "VGun"
  DropItem "GMIDHandGrenadeGiver"
  translation "112:127=%[0, 0, 0]:[2, 0, 0]"
  Tag "$TAG_EVILMARINE3"
  States
  {
  Spawn:
	PLA4 EF 10 A_Look
    Loop
  See:
	PLA4 AABB 2 A_FastChase
	PLA4 C 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	PLA4 CCDD 2 A_FastChase
	PLA4 A 0 A_PlaySound("Cyber/Hoof", CHAN_BODY)	
	Loop
  Pain:
    PLA4 H 4 A_StopSound(CHAN_VOICE)
	PLA4 H 4 A_Pain
	Goto See
  Pain.Foot:
  Pain.BigFist:
    PLA4 H 2 A_FaceTarget
    PLA4 A 0 A_ChangeFlag("NOPAIN",1)
    PLA4 A 0 A_Recoil(14)
    PLA4 H 33 A_Pain
	PLA4 H 0 A_ChangeFlag("NOPAIN",0)
    Goto See
  Pain.Uppercut:
	PLA4 G 0 A_FaceTarget
	PLA4 A 0 A_ChangeFlag("NOPAIN",1)
	PLA4 A 0 ThrustThingZ(0,16,0,0)
	PLA4 G 33 A_Pain
	PLA4 G 0 A_ChangeFlag("NOPAIN",0)
	Goto See
  Missile:
	PLA4 E 10 A_FaceTarget
	PLA4 G 1 bright A_CustomBulletAttack(22.5, 0, 3, random(1,5)*3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	PLA4 E 1
	PLA4 G 1 bright A_CustomBulletAttack(22.5, 0, 3, random(1,5)*3, "XaserPuff_NoTaunt", 0, CBAF_NORANDOM)
	PLA4 E 1 A_SpidRefire
	Goto Missile+1
  Death.AngleGrinder:
	EVC1 A 0 A_GiveToTarget("ScoreItem",250)
	EVC1 A 5 A_Scream
	EVC1 B 0 A_NoBlocking
	EVC1 B 5 A_PlaySound("misc/gibbed",CHAN_AUTO)
	EVC1 C 0 A_FaceTarget
	EVC1 C 5 A_SpawnItemEx("EvilMarineTorso_Minigun",0,0,32,random(-2,-4),random(-1,1),random(2,4),1)
	EVC1 D 5
	EVC1 E 5 A_PlaySound("body/hitfloor")
	EVC1 FG 5
	TNT1 A 0 A_GiveToTarget("PlayerKillTaunt",1)
	EVC1 G -1
	Stop
   }
}

Actor BurningMarine : BurningBoi
{
	States
	{
	Spawn:
		FGUY A 0 Bright A_Look
		Goto See
	See:
		FGUY A 0 A_PlaySound("bgrunt/burn", CHAN_VOICE)
		FGUY A 1 Bright A_Wander
		Goto ContinueBurn
	}
}

Actor EvilMarineTorso_Rifle
{
  Radius 3
  Height 3
  Mass 1
  +MISSILE
  +NOTELEPORT
  +MOVEWITHSECTOR
  +BLOODLESSIMPACT
  +FLOORCLIP
  +NOBLOCKMAP
  -BOUNCEONACTORS
  -SOLID
  Speed 2
  BounceCount 8
  BounceSound "body/hitfloor"
  Translation "112:127=%[0, 0, 0]:[2, 2, 0]"
  States
  {
  Spawn:
	EVC2 A 0
	TNT1 A 0 A_JumpIf(CallACS("CleanerBlood")==1, "NoThankYou")
	EVC2 A 1 A_SpawnItemEx("BloodSplurt",0,0,0,2,random(-2,2),random(-3,3),random(-3,3))
	Loop
  NoThankYou:
	EVC2 A 1
	Loop
  Death:
	EVC2 BCD 5
	EVC2 E -1
	Stop
  }
}

Actor EvilMarineTorso_Shotgun : EvilMarineTorso_Rifle
{
  Translation "112:127=%[0, 0, 0]:[2, 1, 0]"
}

Actor EvilMarineTorso_Minigun : EvilMarineTorso_Shotgun
{
  Translation "112:127=%[0, 0, 0]:[2, 0, 0]"
}