#library "misc"
#include "zcommon.acs"

#define MIN_TID (-32768)
#define PLAYER_MAX 64
#define HUD_DEATHMSG 8888

//This script is pretty much what makes the player taunting not happen every time you kill an enemy (unless you have the mod set up that way in the configuration).
//Based on Duke Nukem: War Against the Doomed by xenoxols
script "PlayerTauntCooldown" (void)
{
	while (CheckInventory("PlayerTauntCooldown") > 0)
	{
		Delay(GetCVar("gmid_tauntcooldown"));
		TakeInventory("PlayerTauntCooldown",1);
	}
}

//This script is to check if the game is being played in a PVP mode.
//This can be used to make sure certain items don't spawn in deathmatch.
//Loosely based on code from Death Foretold by DBT.
script "PVPCheck" (void)
{
	if (GameType() == 2)
	{
	  SetResultValue(2);
	}
}

//This script clears the player of a certain token upon starting the level.
//This is necessary so that the player can only use one Cookie-Crystal per level.
Script "LevelIntro" ENTER
{
	TakeInventory("HasSummonedCookie",1);
	Delay(1);
}

//Plays a warning sound and logs a message to the console when a powerup is about to phase out
//Based on Quake II Stuff by TheCamaleonMaligno
script "PowerupFading" ENTER
{
	Int PNum = PlayerNumber();
	While(PNum==PlayerNumber())
	{
		If(GetActorPowerupTics(0,"PowerBioSuitProtection")==105)
		{
			Log(l:"BIOFADE");
			PlaySound(0,"RADSFADE",0);
		}
		If(GetActorPowerupTics(0,"PowerLightAmp2")==105)
		{
			Log(l:"GOGGLESFADE");
			PlaySound(0,"NVG/Fade",0);
		}
		If(GetActorPowerupTics(0,"PowerInvulnerable2")==105)
		{
			Log(l:"INVULFADE");
			PlaySound(0,"Invulnerability/Fade",0);
		}
		If(GetActorPowerupTics(0,"PowerDoubleDamage")==105)
		{
			Log(l:"DOUBLEFADE");
			PlaySound(0,"DoubleDamage/Fade",0);
		}
		If(GetActorPowerupTics(0,"PowerQuadDamageST")==105)
		{
			Log(l:"QUADFADE");
			PlaySound(0,"QuadDamage/Fade",0);
		}
		If(GetActorPowerupTics(0,"PowerSpeed2")==105)
		{
			Log(l:"HASTEFADE");
			PlaySound(0,"Haste/Fade",0);
		}
		If(GetActorPowerupTics(0,"PowerFlight2")==105)
		{
			Log(l:"BELTFADE");
			PlaySound(0,"Belt/Fade",0);
		}
		Delay(1);
	}
}

//The Master Force Lord has his own theme song when he sees you.
//These two scripts start playing his theme when the player is sighted and when the Master Force Lord dies respectively.
Script "MasterForceLordTheme" (void)
{
	SetMusic("MFLORD1",0);
}

Script "MasterForceLordEnding" (void)
{
	SetMusic("MFLORD2",0);
	Delay(35*3);
	Delay(15);
	SetMusic("*",0);
}

//This script rewards the player with 10,000 points if they kill all monsters on the level.
Script "MonsterBonus" ENTER
{
	int mtotal;
	int mkilled2;
	int mkilled;
	
	mtotal = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
	mkilled2 = mkilled;
	mkilled = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
	
	if (GameType () < 2) //So that the player doesn't get rewarded in Deathmatch
	{
		if (mkilled > mkilled2)
		{
			if (mkilled == mtotal)
			{
				FadeTo(255,0,0,0.25,0);
				PlaySound(0,"secret/allfound",0);
				GiveInventory("ScoreItem",10000);
				PrintBold(l:"KILLALL");
				FadeTo(0,0,0,0.0,1.0);
			}
		}
		Delay(1);
		Restart;
	}
}

//Ditto, but for items.
Script "ItemBonus" ENTER
{
	int itotal;
	int ipicked2;
	int ipicked;
	
	itotal = GetLevelInfo(LEVELINFO_TOTAL_ITEMS);
	ipicked2 = ipicked;
	ipicked = GetLevelInfo(LEVELINFO_FOUND_ITEMS);
	
	if (GameType () < 2)
	{
		if (ipicked > ipicked2)
		{
			if (ipicked == itotal)
			{
				FadeTo(0,0,255,0.25,0);
				PlaySound(0,"secret/allfound",0);
				GiveInventory("ScoreItem",10000);
				PrintBold(l:"PICKUPALL");
				FadeTo(0,0,0,0.0,1.0);
			}
		}
		Delay(1);
		Restart;
	}
}

//This script tells the player how many secret areas a left in the level, and rewards with 10,000 points when all are found.
//Loosely based on Samsara by TerminusEst13 and Kinsie
Script "SecretBonus" ENTER
{
	int stotal;
	int sfound2;
	int sfound;

	stotal = GetLevelInfo(LEVELINFO_TOTAL_SECRETS);
	sfound2 = sfound;
	sfound = GetLevelInfo(LEVELINFO_FOUND_SECRETS);

	if (GameType () < 2)
	{
		if (sfound > sfound2)
		{
			if (sfound == stotal) //If the player has found all secrets, give the player 10,000 points
			{
				FadeTo(255,255,0,0.25,0);
				PlaySound(0,"secret/allfound",0);
				GiveInventory("ScoreItem",10000);
				PrintBold(l:"SECRETALL");
				FadeTo(0,0,0,0.0,1.0);
			}
			Else
			{
				PlaySound(0,"secret/found",0);
				if (stotal-sfound > 1) //Print plural message if 2 or more secrets are left
				{
					Print(l:"SECRETAREA",d:stotal-sfound,l:"SECRETREMAIN");
				}
				Else
				Print(l:"SECRETAREA",l:"SECRETSINGLE");
			}
		}
		Delay(1);
		Restart;
	}
}

//This script (which is a hacky workaround, I know) checks if the player is playing with the 1-Up Music Pack.
//It is intended to be used with the Master Force Lord to prevent his theme from playing if 1-Up is loaded.
//(I can't believe I used H-Doom for the necessary function...)
function bool ClassExists(str class)
{
	int tid = UniqueTID(MIN_TID, 0);
	if (SpawnForced(class, 0, 0, 0, tid))
	{
		Thing_Remove(tid);
		return true;
	}
	return false;
}

script "1UpCheck" (void)
{
	if (ClassExists("1UpMusicPackToken"))
	{
	  SetResultValue(2);
	}
}

Script "SkullBombFace" (void)
{
  SetHudSize(320,200,0);
  FadeTo(0,0,0,1.0,0);
  SetFont("SKULBOMB");
  HudMessage(s:"A"; HUDMSG_FADEOUT, 0, CR_UNTRANSLATED,0.1,0.5,0.0,3.0);
  FadeTo(0,0,0,0.0,3.0);
  SetHudSize(0,0,0);
}

//This script is used to check if the player is holding down certain inputs when using their fist attacks
Script "MeleeInputCheck" (void)
{
	int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
	//Initiates uppercut
	if (buttons & BT_CROUCH)
	{
	  SetResultValue(789);
	}
	//Initiates right hook
	else if (buttons & BT_MOVELEFT)
	{
	  SetResultValue(456);
	}
	//Initiates left hook
	else if (buttons & BT_MOVERIGHT)
	{
	  SetResultValue(123);
	}
}

//These scripts print a messaqe on the screen when the player dies
//Based on The Adventures of Square by BigBrik Games
Script "GameOverMan" DEATH
{
	str randomGameOver = "";
	switch (GameType())
	{
		case GAME_SINGLE_PLAYER:
			randomGameOver = StrParam(s:"GAMEOVERA",i:random(1,6));
			HudMessage(l:randomGameOver,s:"\n",l:"GAMEOVERB1"; HUDMSG_PLAIN, HUD_DEATHMSG, CR_GOLD, 1.5, 0.75, 0.0);
			break;
		case GAME_NET_COOPERATIVE:
		case GAME_NET_DEATHMATCH:
			HudMessage(l:"GAMEOVERB2"; HUDMSG_PLAIN, HUD_DEATHMSG, CR_GOLD, 1.5, 0.75, 0.0);
			break;
		case GAME_TITLE_MAP:
			break;
	}
}

function void AssignPlayerTID(void)
{
	int player_tags[PLAYER_MAX];
	int pid = PlayerNumber();
	if (pid < 0)
		return;

	int ptid = UniqueTID();
	player_tags[pid] = ptid;
	// activator is player
	Thing_ChangeTID(0, ptid);
}

script "RespawnThePlayer" RESPAWN
{
	// Clear HUD message
	HudMessage(s:""; HUDMSG_PLAIN, HUD_DEATHMSG, CR_GOLD, 200.0, 120.0, 0.0);
	// reassign TID
	AssignPlayerTID();
}