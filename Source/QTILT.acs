/*
 * GZDoom Strafing Camera Tilting
 * Copyright (C) 2015 Nash Muhandes
 *
 * This program is free software: you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by the Free
 * Software Foundation, either version 3 of the License, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of  MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
 * more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// Feel free to do whatever you want with this, though credits would be
// appreciated! You don't need to ask for my permission to use this code
// in your mods.
// Nash

#include "zcommon.acs"
#library "qtilt"

//===========================================================================
//
// UTILITY FUNCTIONS
//
//===========================================================================

// player is on ground?
function int CheckPlayerOnGround(void)
{
	int onground;
	if (
		GetActorZ (0) == GetActorFloorZ (0) 
		|| 
		GetActorVelZ(0) == 0 
			&& 
			!Spawn(
				"Z_FloorCheck",
				GetActorX(0),
				GetActorY(0),
				GetActorZ(0) - (4 << 16), 0, GetActorAngle(0)
			)
		)
	{
		onground = 1;
		//SetFont("SmallFont");
		//Print(s: "on ground");
	}
	else
	{
		onground = 0;
		//SetFont("SmallFont");
		//Print(s: "in air");
	}
	return onground;
}

// get player velocity
function int GetPlayerMoveSpeed(int p)
{
	return VectorLength(GetActorVelX(p), GetActorVelY(p));
}

// input checking
function bool KeyUp(int key)
{
    return !(GetPlayerInput(-1, INPUT_BUTTONS) & key);
}

function bool KeyDown(int key)
{
    return !!(GetPlayerInput(-1, INPUT_BUTTONS) & key);
}


//===========================================================================
//
// STRAFE TILTING
//
//===========================================================================

int dostrafetilt = 1;

Script "Script_StrafeTilt" ENTER
{
	while(1)
	{
		if (dostrafetilt)
		{
			int strafemod;
			int strafe;
			static int strafespeed = 64;

			// receive input
			if (CheckPlayerOnGround() && GetPlayerMoveSpeed(PlayerNumber()) > 1.1) // only fast movement will cause tilt
			{
				strafemod = GetPlayerInput(-1, INPUT_SIDEMOVE) / 40;
			}
			else
			{
				// reset view
				if (strafe < 0)
					strafe += strafespeed;

				if (strafe > 0)
					strafe -= strafespeed;
			}

			// tilt left
			if (strafemod < 0 && strafe >= strafemod)
			{
				strafe -= strafespeed;
			}

			// tilt right
			if (strafemod > 0 && strafe <= strafemod)
			{
				strafe += strafespeed;
			}

			// reset view
			// when no movement keys are pressed or if player starts to move slow
			if(CheckPlayerOnGround()) {
				if (!KeyDown(BT_MOVELEFT) && !KeyDown(BT_MOVERIGHT) && GetPlayerInput(-1, INPUT_SIDEMOVE) == 0 || GetPlayerMoveSpeed(0) < 1.1)
				{
					if (strafe < 0)
						strafe += strafespeed;

					if (strafe > 0)
						strafe -= strafespeed;
				}
			}

			// tilt!
			ChangeActorRoll(0, strafe, TRUE);
		}

		Delay(1);
	}
}